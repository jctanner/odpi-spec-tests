// http://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing-with-the-testsets-plugin/
// https://github.com/pkainulainen/gradle-examples/tree/master/testsets-plugin

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.0.2'
    }
}

apply plugin: 'application'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'java'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.1'
    compile 'log4j:log4j:1.2.17'
    testCompile 'junit:junit:4.11'
    integrationTestCompile 'org.assertj:assertj-core:3.0.0'
}

project.integrationTest {
    outputs.upToDateWhen { false }
}

//Ensure that the check task fails the build if there are failing integration tests.
check.dependsOn integrationTest

//Ensure that our unit tests are run before our integration tests
integrationTest.mustRunAfter test

//Ensure that the HTML reports of unit and integration tests are written to different directories.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

mainClassName = 'org.opdi.spectests'

